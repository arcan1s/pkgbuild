# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: <giacomogiorgianni at gmail dot com>

pkgname=katecpp-helper-plugin
_pkgname=KateCppHelperPlugin
pkgver=1.0.2
pkgrel=1
pkgdesc="Kate C++ Helper plugin"
arch=('i686' 'x86_64')
url="https://github.com/zaufi/kate-cpp-helper-plugin"
license=('GPL')
depends=('boost' 'clang>=3.3' 'kdesdk-kate' 'xapian-core')
makedepends=('automoc4' 'cmake')
source=("http://kde-apps.org/CONTENT/content-files/148606-${_pkgname}-${pkgver}.tar.bz2")
md5sums=('def3d81cff1e3bc4cc107a5d81ef158e')

build() {
  rm -rf "${srcdir}/build"
  mkdir "${srcdir}/build"
  cd "${srcdir}/build"

  cmake -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
        -DBUILD_TESTING=OFF \
        -DNO_DOXY_DOCS=ON \
        -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` \
        "../${_pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
